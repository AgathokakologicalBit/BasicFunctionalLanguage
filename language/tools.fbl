: include "core.fbl"


` Convertion tools
: ( def "bool" [ value ] __if value true false )
: ( def "bti" [ value ] value 1 0 )
: ( def "bts" [ value ] value "true" "false" )


` Boolean logic
: ( def "not" [ a ] a false true )

: ( def "and" [ a ] [ b ] a b a )
: ( def "or"  [ a ] [ b ] a a b )
: ( def "xor" [ a ] [ b ] a (not b) b )


` Utility functions
: ( def "compose" [ f ] [ g ] [ arg ] f ( g arg ))
: ( def "@" compose )
: ( def "arg_swap" [ function ] [ a ] [ b ] function b a )
: ( def "power" [ _power ] [ function ] [ value ] run (
    __if _power
        ([ --- ] function (power (- _power 1) function value))
        ([ --- ] value)
))

` Printing
: ( def "pbool" [ value ] @ print bts value )

` Working with numbers
: ( def "is_positive" [ number ] bool (+ number (abs number)) )
: ( def "is_negative" [ number ] bool (- number (abs number)) )
: ( def "is_zero"     [ number ] not (bool number) )
: ( def "sign"        [ number ] - (@ bti is_positive number) (@ bti is_negative number) )

: ( def "is_greater"  [ a ] [ b ] is_positive (- a b) )
: ( def "is_less"     [ a ] [ b ] is_negative (- a b) )
: ( def "is_equal"    [ a ] [ b ] is_zero     (- a b) )


` Iteration tools
: ( def "loop" [ times ] [ code ]
    : ( def "__loop" [ from ] [ times ] [ code ]
        : (__if times   code void) (- from times)
        : (__if times __loop void) from (- times 1) code
    )
    : run (@ if is_positive times ([ --- ] __loop (int times) (int times) code) void)
)


: ( def "while" [ condition ] [ code ]
    : run (@ if run condition ([ --- ] run code : while condition code) void)
)
